/*
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *    http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

plugins {
    id 'java-library'
    id 'maven-publish'
    id 'signing'
}

repositories {
    mavenCentral()
}

dependencies {
    compileOnly 'javax.servlet:javax.servlet-api:3.1.0'
    
    implementation project(':httprpc-client')
}

sourceSets {
  main {
    java {
        java {
            srcDirs = ['../httprpc/src/main/java']
        }

        resources {
            srcDirs = ['../httprpc/src/main/resources']
        }
        
        include 'org/httprpc/WebService.java'
        include 'org/httprpc/RequestMethod.java'
        include 'org/httprpc/RequestParameter.java'
        include 'org/httprpc/ResourcePath.java'
        include 'org/httprpc/Response.java'
        include 'org/httprpc/io/TemplateEncoder.java'
        include 'org/httprpc/io/XMLEncoder.java'
        include 'org/httprpc/sql/**'
    }
  }
}

jar {
    manifest {
        attributes (
            'Implementation-Title': project.name, 
            'Implementation-Version': project.version
        )
    }
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allJava
    classifier = 'sources'
}

task javadocJar(type: Jar) {
    from javadoc
    classifier = 'javadoc'
}

publishing {
    repositories {
        maven {
            url = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"

            credentials {
                username nexusUsername
                password nexusPassword
            }
        }
    }

    publications {
        mavenJava(MavenPublication) {
            artifactId = 'httprpc-server'
            from components.java
            artifact sourcesJar
            artifact javadocJar
            pom {
                name = project.name
                description = 'Lightweight REST for Java'
                url = 'http://httprpc.org'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        name = 'Greg Brown'
                        email = 'gk_brown@icloud.com'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/gk-brown/HTTP-RPC.git'
                    url = 'https://github.com/gk-brown/HTTP-RPC/tree/master/httprpc'
                }
            }
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}
